Checkers(
[
 define(GenericExceptionCatchClause,exceptions,"catching generic exception",
        BT_TYPE_RULESET,
        ruleset(
          Catch(FormalParameter($m,Identifier("Exception"),$x),$block) 
                         -> true [ violationDiscovered(GenericExceptionCatchClause,"generic exception catch clause",$x) ]
        ),
        true
  ),

 define(EmptyCatchBlock,exceptions,"empty catch block",
        BT_TYPE_RULESET,
        ruleset(
          Catch($x,Block(NIL)) 
                         -> true [ violationDiscovered(EmptyCatchBlock,"empty catch block",$x) ]
        ),
        true
  ),

 define(GenericExceptionThrow,exceptions,"throwing of generic exception",
        BT_TYPE_RULESET,
        ruleset(
          ThrowStatement(AllocationExpression(Identifier("Exception"),$dims,$args,$body))
                         -> true [ violationDiscovered(GenericExceptionThrow,"throwing of generic exception",$args) ]
        ),
        true
  ),

 define(NotOurPackaging,style,"package names must start with ua.gradsoft ",
        BT_COMPILATION_UNIT_RULESET,
        ruleset(
          PackageDeclaration(Name($x)) -> COM_MYCOMPANY($x) ,
          COM_MYCOMPANY([$x:[$y:$z]]) 
              [ $x==Identifier("ua") && $y==Identifier("gradsoft") ] -> true 
              !-> false [ violationDiscovered(NotOurPackaging,"Bad package name", $x) ]
        ),
        false
  ),

 define(UnclosedCloseable,resources,"unclosed instance of closeable",
        MODEL_RULESET,
        ruleset(
          import(general,apply),
          import(general,logical_or),
          import(general,logical_and),

          UnclosedCloseable($x) -> checkNoOpen(apply(TrackOpenClose,$x)),

          checkNoOpen([]) -> true,
          checkNoOpen([$x:$y]) -> checkNoOpen($x) && checkNoOpen($y),
          checkNoOpen(CHANGE($x,OPEN,$where)) -> false [ violationDiscovered(UnclosedCloseable,"closeable variable does not closed",$where) ],

          checkNoOpen($x && $y) -> checkNoOpen($x) && checkNoOpen($y),
          checkNoOpen($x || $y) -> checkNoOpen($x) || checkNoOpen($y),
          checkNoOpen(Q($x)) -> checkNoOpen($x)
                    

        ),
        true
  )



]
);
